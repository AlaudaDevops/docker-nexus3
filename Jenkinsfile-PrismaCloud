@Library(['private-pipeline-library', 'jenkins-shared']) _

import com.sonatype.jenkins.pipeline.OsTools
import groovy.json.JsonSlurper

IQ_URL_BASE = "https://sonatype.sonatype.app/platform"
REPO_BASE_URL = "https://repo.sonatype.com/service/rest"
TARGET_REPO_NAME = "sonatype-sboms"
CYCLONEDX_VERSION = "1.5"

properties([
    parameters([
        string(name: 'BRANCH_TO_BUILD', defaultValue: '',
            description: 'Branch the script will be loaded from'),
        string(name: 'DOCKER_IMAGE_TAG', defaultValue: 'latest',
            description: 'Tag of the Docker image to be scanned'),
        string(name: 'NXRM_Version', defaultValue: '',
            description: 'Nexus Repository Manager Version. The result SBOMs will be tagged with this version.'),
        string(name: 'UBI_IMAGE_TAG', defaultValue: 'latest',
            description: 'Tag of the UBI image to be scanned')
    ])
])

def getComponentSbom(String buildDir, String componentName, String componentVersion) {
    def componentId = getComponentInfo(componentName).applications[0].id
    withCredentials([usernamePassword(credentialsId: 'jenkins-saas-service-acct', usernameVariable: 'IQ_USER', passwordVariable: 'IQ_PASSWORD')]) {
        def formats = ['spdx', 'cyclonedx']
        formats.each { format ->
            def urlPath = format == 'spdx' ? "spdx/${componentId}/stages/release?format=json" : "cycloneDx/${CYCLONEDX_VERSION}/${componentId}/stages/release"
            sh "curl -s -L -u \$IQ_USER:\$IQ_PASSWORD -o '${buildDir}/${format}/${componentName}-${componentVersion}-${format}.json' -X GET -H 'Accept: application/json' '${IQ_URL_BASE}/api/v2/${urlPath}'"
            sh "jq . ${buildDir}/${format}/${componentName}-${componentVersion}-${format}.json > ${buildDir}/${format}/${componentName}-${componentVersion}-${format}-formatted.json"
            sh "mv ${buildDir}/${format}/${componentName}-${componentVersion}-${format}-formatted.json ${buildDir}/${format}/${componentName}-${componentVersion}-${format}.json"
        }
    }
}

def getComponentInfo(String componentName) {
    def jsonSlurper = new JsonSlurper()
    def response = null

    withCredentials([
        usernamePassword(
            credentialsId: 'jenkins-saas-service-acct',
            usernameVariable: 'IQ_USER',
            passwordVariable: 'IQ_PASSWORD')
    ]) {
        def rawResponse = sh(returnStdout: true, script: "curl -s -u \$IQ_USER:\$IQ_PASSWORD -X GET '${IQ_URL_BASE}/api/v2/applications?publicId=${componentName}'")
        response = jsonSlurper.parseText(rawResponse)
    }
    return response
}

def publishComponent(String buildDir, String componentName, String componentVersion) {
    def publishCommand = """
    curl -v -u \$NXRM_USER:\$NXRM_PASSWORD -X POST '${REPO_BASE_URL}/v1/components?repository=${TARGET_REPO_NAME}' \
    -F 'raw.directory=/PrismaCloud/${componentName}/${componentVersion}/' \
    -F 'raw.asset1=@${buildDir}/${componentName}-prisma-cloud-scan-results.json' \
    -F 'raw.asset1.filename=${componentName}-${componentVersion}-prisma-cloud-scan-results.json'
    """
    withCredentials([
        usernamePassword(
            credentialsId: 'sonatype-sbom-deployer',
            usernameVariable: 'NXRM_USER',
            passwordVariable: 'NXRM_PASSWORD')
    ]) {
        sh(publishCommand)
    }
}

def scanAndCopyResults(String image, String resultsFileName) {
    prismaCloudScanImage(
        ca: '',
        cert: '',
        dockerAddress: 'unix:///var/run/docker.sock',
        ignoreImageBuildTime: true,
        image: image,
        key: '',
        logLevel: 'debug',
        podmanPath: '',
        project: '',
        resultsFile: "${env.buildDir}/${resultsFileName}"
    )
    sh "jq . ${env.buildDir}/${resultsFileName} > ${env.buildDir}/${resultsFileName}-formatted.json"
    sh "mv ${env.buildDir}/${resultsFileName}-formatted.json ${env.buildDir}/${resultsFileName}"
    sh "cp ${env.buildDir}/${resultsFileName} ${resultsFileName}"
    sh "ls -la ${env.buildDir}"
}

pipeline {
    agent any
    environment {
        buildDir = "./.sbom-build/job-${env.BUILD_NUMBER}"
    }
    stages {
        stage('Checkout') {
            steps {
                git branch: params.BRANCH_TO_BUILD, url: 'https://github.com/sonatype/docker-nexus3.git'
            }
        }
        stage('Build Image') {
            steps {
                script {
                    runSafely("docker build -t docker-nexus3:${params.DOCKER_IMAGE_TAG} .")
                }
            }
        }
        stage('Analyze Images with Prisma Cloud') {
            steps {
                script {
                    sh "mkdir -p ${env.buildDir}/spdx && mkdir -p ${env.buildDir}/cyclonedx"
                    echo "Analyzing docker-nexus3 image with Prisma Cloud"
                    scanAndCopyResults("docker-nexus3:${params.DOCKER_IMAGE_TAG}", "docker-nexus3-prisma-cloud-scan-results.json")

                    def ubiImage = "registry.access.redhat.com/ubi8/ubi-minimal:${params.UBI_IMAGE_TAG}"
                    sh "docker pull ${ubiImage}"
                    echo "Analyzing UBI image with Prisma Cloud"
                    scanAndCopyResults(ubiImage, "ubi-minimal-prisma-cloud-scan-results.json")
                }
            }
        }
        stage('Publish Scan Results') {
            steps {
                script {
                    publishComponent(env.buildDir, "docker-nexus3", params.DOCKER_IMAGE_TAG)
                    publishComponent(env.buildDir, "ubi-minimal", params.UBI_IMAGE_TAG)
                }
            }
        }
    }
    post {
        always {
            prismaCloudPublish resultsFilePattern: "${env.buildDir}/docker-nexus3-prisma-cloud-scan-results.json"
            prismaCloudPublish resultsFilePattern: "${env.buildDir}/ubi-minimal-prisma-cloud-scan-results.json"

            prismaCloudPublish resultsFilePattern: 'docker-nexus3-prisma-cloud-scan-results.json'
            prismaCloudPublish resultsFilePattern: 'ubi-minimal-prisma-cloud-scan-results.json'
            archiveArtifacts artifacts: "docker-nexus3-prisma-cloud-scan-results.json", fingerprint: true
            archiveArtifacts artifacts: "ubi-minimal-prisma-cloud-scan-results.json", fingerprint: true

            script {
                OsTools.runSafe(this, "rm -rf '${env.buildDir}'")
            }
        }
    }
}
